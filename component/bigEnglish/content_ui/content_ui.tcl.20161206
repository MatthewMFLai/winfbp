namespace eval content_ui {

variable m_map
variable m_localmap

proc init {} {
    variable m_map
    variable m_localmap

    if {[info exists m_map]} {
	unset m_map
    }
    array set m_map {}
    if {[info exists m_localmap]} {
	unset m_localmap
    }
    array set m_localmap {}
    return
}

proc extract_words {data} {
    regsub -all "\n" $data " " data
    regsub -all "\"" $data " " data

    foreach word $data {
        set word [string tolower $word]
    	if {[string is alpha $word]} {
	    lappend wordlist $word
    	} else {
	    set newword ""
	    set max [string length $word]
	    set i 0
	    while {$i < $max} {
	    	set char [string index $word $i]
	    	if {[string is alpha $char] ||
		    $char == "-" ||
		    $char == "-"} {
		    append newword $char
	    	}
	        incr i
	    }
	    if {$newword != ""} {
	    	lappend wordlist $newword
	    }
    	}
    }
    set wordlist [lsort -unique $wordlist]
    return $wordlist
}

proc map_set {word meanings} {
    variable m_map

    if {$meanings == ""} {
	return
    }

    if {![info exists m_map($word)]} {
	set m_map($word) $meanings
    }
    return
}

proc map_get {word} {
    variable m_map

    set rc ""
    if {[info exists m_map($word)]} {
	set rc $m_map($word)
    }
    return $rc
}

proc map_get_words {} {
    variable m_map
    return [array names m_map] 
}

proc map_clear {word} {
    variable m_map
    if {[info exists m_map($word)]} {
	unset m_map($word)
    }
    return
}

proc localmap_default {word idxlist} {
    variable m_localmap

    foreach idx $idxlist {
	set m_localmap($word,$idx) 0
    }
    return
}

proc localmap_get {word idx} {
    variable m_localmap

    set rc ""
    if {[info exists m_localmap($word,$idx)]} {
	    set rc $m_localmap($word,$idx)
    }
    return $rc
}

proc localmap_get_all {} {
    variable m_localmap

    set rc ""
    foreach index [array names m_localmap] {
	set offset $m_localmap($index)
	set word [lindex [split $index ","] 0]
	set idx [lindex [split $index ","] 1]
	set meanings [map_get $word]
	if {$meanings == ""} {
	    continue
	}
	lappend rc [list $word $idx [lindex $meanings $offset]]
    }
    return $rc
}

# Similar to localmap_get_all except only return the words without
# the repeating meanings.
# i.e. if we have {woods,5.64 0} {woods,6.21 0} then return the
# first occurence, that is, {woods,5.64 0} only.

proc localmap_get_all_no_repeat {} {
    variable m_localmap

    set rc ""
    set indexlist ""
    array set tmptable {}
    foreach index [lsort [array names m_localmap]] {
	set offset $m_localmap($index)
	set word [lindex [split $index ","] 0]
	if {![info exists tmptable($word,$offset)]} {
	    set tmptable($word,$offset) 1
	    lappend indexlist $index
	}	
    }

    foreach index $indexlist {
	set offset $m_localmap($index)
	set word [lindex [split $index ","] 0]
	set idx [lindex [split $index ","] 1]
	set meanings [map_get $word]
	if {$meanings == ""} {
	    continue
	}
	lappend rc [list $word $idx [lindex $meanings $offset]]
    }
    return $rc
}

proc localmap_set {word idx offset} {
    variable m_localmap

    set m_localmap($word,$idx) $offset
    return
}

proc localmap_clear {word} {
    variable m_localmap

    foreach index [array names m_localmap "$word,*"] {
    	unset m_localmap($index)
    }
    return
}

proc map_save {mapfile} {
    variable m_map
    variable m_localmap

    set fd [open $mapfile w]
    puts $fd [array get m_map]
    puts $fd [array get m_localmap]
    close $fd 
    return
}

proc map_load {mapfile} {
    variable m_map
    variable m_localmap

    if {![file exists $mapfile]} {
	return
    }

    set fd [open $mapfile r]
    if {[gets $fd mapdata] > -1} {
    	if {[info exists m_map]} {
	    unset m_map
    	}
    	array set m_map $mapdata
    }

    if {[gets $fd mapdata] > -1} {
    	if {[info exists m_localmap]} {
	    unset m_localmap
    	}
    	array set m_localmap $mapdata
    }

    close $fd
    return
}

}

# bigEnglish.tcl --
#
# This demonstration script creates a text widget that describes
# the basic editing functions.
#
# To get the index of the current selection in text widget
# $w.frame0.text dump -mark insert # mark insert 1.516
#

proc clean_word {word} {
    set word [string tolower $word]
    if {[string index $word end] == ":"} {
	set word [string range $word 0 end-1]
    }
    return $word
}

proc word_rules {word} {
    if {$word == " " ||
        $word == "." ||
        $word == "," ||
        $word == "?" ||
        $word == "\"" ||
        $word == "\'" ||
        $word == "\[" ||
        $word == "\]" ||
        $word == "\{" ||
        $word == "\}" ||
        $word == "\n" ||
        $word == "!" ||
        $word == ":" ||
        $word == ";" ||
        $word == "$"} {
    	return 1
    } else {
    	return 0
    }
}

proc get_meaning {item} {
   send_request $item OUT-1
   server_async_send ""
   return
}

proc trim_meanings {meanings} { 
   # Custom processing specific to the Yahoo English to Chinese Dict
   # Remove the "1." from each meaning phrase.
   # Remove the square bracket contents.
   if {$meanings != ""} {
	set tmp_meanings ""
	foreach meaning $meanings {
	    set index [string first "\[" $meaning]
	    if {$index > 0} {
	    	set index2 [string last "\]" $meaning]
		set meaning [string replace $meaning $index $index2]
	    }
	    set index [string first " " $meaning]
	    incr index
	    lappend tmp_meanings [string range $meaning $index end]
	}
	return $tmp_meanings
    } else {
    	return ""
    }
}

proc set_meaning {item meanings w} {
    #set meanings [trim_meanings $meanings]
	
    content_ui::map_set $item $meanings

    # Attach meaning to the stand-alone word, not to the word
    # imbedded inside another word. Hence check for white space before and
    # after the word.
    set idxlist_new ""
    set idxlist [$w.frame0.text search -all -nocase $item 0.0]
    foreach idx $idxlist {
    	set before [$w.frame0.text get $idx-1c]	
    	set after [$w.frame0.text get $idx+[string length $item]c]
	if {[word_rules $before] && [word_rules $after]} {
	    lappend idxlist_new $idx
	}
    }
    content_ui::localmap_default $item $idxlist_new
    return $meanings
}

package require Tk

tk_setPalette "LavenderBlush3"

set w .text
catch {destroy $w}
toplevel $w
wm title $w "Text Demonstration - Basic Facilities"
wm iconname $w "text"

frame $w.mbar -borderwidth 1 -relief raised
pack $w.mbar -fill x
menubutton $w.mbar.file -text "File" -menu $w.mbar.file.m
pack $w.mbar.file -side left
menu $w.mbar.file.m
$w.mbar.file.m add command -label "Open" -command {
    set filename [tk_getOpenFile]
    if {$filename != ""} {
	set fd [open $filename r]
	set data [read $fd]
	close $fd

	# Back up text data.
	set g_text_backup $data

	set wordlist [content_ui::extract_words $data]
	$w.frame0.text delete 0.0 end 
	$w.frame.list delete 0 end 
	$w.frame2.list delete 0 end 
	$w.frame3.list delete 0 end 
	$w.frame0.text insert 0.0 $data
         	
    	$w.frame.list delete 0 end
        foreach word $wordlist {	
    	    $w.frame.list insert end $word
	}

	# Init the dictionary cache.
	content_ui::init
    }
    unset filename
}
$w.mbar.file.m add command -label "Load Data" -command {
    set filename [tk_getOpenFile]
    if {$filename != ""} {
	content_ui::map_load $filename

        # Update frame and frame2 display
        foreach word [lsort [content_ui::map_get_words]] {
	    $w.frame2.list insert end $word	
    	    set idx [lsearch [$w.frame.list get 0 end] $word]
	    if {$idx != -1} {
    		$w.frame.list delete $idx
	    }
	}
    }
}
$w.mbar.file.m add command -label "Save" -command {
    set filename [tk_getSaveFile]
    if {$filename != ""} {
	set fd [open $filename w]
	puts $fd [$w.frame0.text get 0.0 end]
	close $fd
    }
}
$w.mbar.file.m add command -label "Save Data" -command {
    set filename [tk_getSaveFile]
    if {$filename != ""} {
    	content_ui::map_save $filename
    }
}
$w.mbar.file.m add command -label "Save Content" -command {

set depth 4 
set doctype "parser_default"
set data [$w.frame0.text get 0.0 end]

# Get title and author.
set exp {Title: (.*?)\n}
regexp $exp $data -> title

set exp {Author: (.*?)\n}
regexp $exp $data -> author 

if {[info exists title] &&
    [info exists author]} {
 
    send_content_save_rqst Add_Title_Author $title $author 0 OUT-3

    array set tmpdata {}
    set curline ""

    foreach line [split $data "\n"] {
    	if {$line != ""} {
	    append curline " $line"
	    continue
    	}

    	if {$curline == ""} {
	    continue
    	}

    	set line [string trim $curline]
    	set curline ""

    	set tmpdata(line) $line
    	set tmpdata(depth) $depth
    	set tmpdata(doctype) $doctype
    	set tmpdata(title) $title
    	set tmpdata(proc) "send_content_save_rqst"
    	set tmpdata(outport) OUT-3
    	Fsm::Run content_fsm tmpdata
    }

    if {$curline != ""} {
    	set line [string trim $curline]
    	set tmpdata(line) $line
    	set tmpdata(depth) $depth
    	set tmpdata(doctype) $doctype
    	set tmpdata(title) $title
    	set tmpdata(proc) "send_content_save_rqst"
    	set tmpdata(outport) OUT-3
    	Fsm::Run content_fsm tmpdata
    }

    if {[Fsm::Is_In_Service content_fsm] != 1} {
        puts "fsm error: [Fsm::Get_Error content_fsm]"
    }
    server_async_send ""
}

}
$w.mbar.file.m add command -label "Reset" -command {
    reset_all [$w.frame0.text get 0.0 end]
}
$w.mbar.file.m add command -label "Exit" -command {
    #exit
}

menubutton $w.mbar.gen -text "Generate" -menu $w.mbar.gen.m
pack $w.mbar.gen -side left
menu $w.mbar.gen.m
$w.mbar.gen.m add command -label "Process" -command {
    array set tmparray {}
    array set tmparray2 {}
    array set tmparray3 {}
    foreach tokens [content_ui::localmap_get_all_no_repeat] {
	set word [lindex $tokens 0]
	set idx [lindex $tokens 1]
	set meaning [lindex $tokens 2]
	set tmparray($idx) $meaning
	set tmparray2($idx) [string length $word]

	# Collect the words.
	if {![info exists tmparray3($word)]} {
	    set tmparray3($word) ""
	}
	lappend tmparray3($word) $meaning	
    }
    foreach idx [lsort -dictionary -decreasing [array names tmparray]] {
	if {[catch {$w.frame0.text insert $idx+$tmparray2($idx)c \($tmparray($idx)\)} rc]} {
	    puts "problem with $tmparray($idx)"
	}
    }
    unset tmparray
    unset tmparray2

    # Output the list of words at the end of the article.
    $w.frame0.text insert end "\n"
    $w.frame0.text insert end "\n"
    foreach word [lsort [array names tmparray3]] {
	$w.frame0.text insert end "$word [lsort -unique $tmparray3($word)] \n"
    }
    unset tmparray3 
}
$w.mbar.gen.m add command -label "Revert" -command {
    $w.frame0.text delete 0.0 end 
    $w.frame0.text insert 0.0 $g_text_backup
}

######################################

frame $w.frameG
label $w.frameG.labA -text "Title"
entry $w.frameG.entA -textvar A -width 50
button $w.frameG.content -text "CONTENT" -command {
    set depth 4
    if {$A != ""} {
    	$w.frame0.text delete 0.0 end 
    	send_content_read_rqst  "Get_All_Token" $A $depth OUT-2
   	server_async_send ""
    }
}
pack $w.frameG
pack $w.frameG.labA $w.frameG.entA $w.frameG.content -in $w.frameG -side left

######################################

frame $w.frame0 -borderwidth 10
pack $w.frame0 -side top -expand yes -fill y

text $w.frame0.text -yscrollcommand [list $w.frame0.scroll set] -setgrid 1 \
	-height 30 -undo 1 -autosep 1
scrollbar $w.frame0.scroll -command [list $w.frame0.text yview]
$w.frame0.text tag configure t_underline -underline 1
pack $w.frame0.scroll -side right -fill y
pack $w.frame0.text -expand yes -fill both

bind $w.frame0.text <Double-1> {
    set word [$w.frame0.text get "insert wordstart" "insert wordend"]
    set idx [$w.frame0.text index "insert wordstart"] 
    # The word may have punctuation at the end, like a :
    # Need to remove the punctuation, and also the upper case.
    set word [clean_word $word]
    set offset [content_ui::localmap_get $word $idx]
    if {$offset != ""} {
    	set meanings [content_ui::map_get $word]
    	$w.frame3.list delete 0 end
    	foreach mean $meanings {
    	    $w.frame3.list insert end $mean
    	}
    	$w.frame3.list selection clear active
    	$w.frame3.list selection set $offset
	set g_last_word $word
	set g_last_idx $idx
    } else {
	# The word is most likely not yet exist in the dictionary.
	# Add the word to the dictionary.
	# Use the same logic for double clicking an entry in frame 1.
    	set idx [lsearch [$w.frame.list get 0 end] $word]
    	$w.frame2.list insert end $word
    	$w.frame.list delete $idx

    	get_meaning $word

	set g_last_word $word
	set g_last_idx $idx
    } 
}

frame $w.frame -borderwidth 10
pack $w.frame -side left -expand yes -fill y

scrollbar $w.frame.scroll -command "$w.frame.list yview"
listbox $w.frame.list -yscroll "$w.frame.scroll set" \
	-width 20 -height 16 -setgrid 1
pack $w.frame.list $w.frame.scroll -side left -fill y -expand 1

bind $w.frame.list <Double-1> {
    set item [selection get]
    set idx [lsearch [$w.frame.list get 0 end] $item]
    $w.frame2.list insert end $item 
    $w.frame.list delete $idx
    
    # Get dictionary meaning.
    set meanings [content_ui::map_get $item]
    if {$meanings == ""} {
    	get_meaning $item
    }
}

frame $w.frame2 -borderwidth 10
pack $w.frame2 -side left -expand yes -fill y

scrollbar $w.frame2.scroll -command "$w.frame2.list yview"
listbox $w.frame2.list -yscroll "$w.frame2.scroll set" \
	-width 20 -height 16 -setgrid 1
pack $w.frame2.list $w.frame2.scroll -side left -fill y -expand 1

bind $w.frame2.list <ButtonRelease-1> {
    set item [selection get]
    set meanings [content_ui::map_get $item]
    $w.frame3.list delete 0 end
    foreach mean $meanings {
    	$w.frame3.list insert end $mean
    }
    
    # Underline the word in the text box.
    $w.frame0.text tag delete t_underline
    $w.frame0.text tag configure t_underline -underline 1
    foreach idx [$w.frame0.text search -all -nocase $item 0.0] {
    	$w.frame0.text tag add t_underline $idx $idx+[string length $item]c
    }
    $w.frame0.text see $idx
}

bind $w.frame2.list <ButtonRelease-3> {
    set item [selection get]
    set idx [lsearch [$w.frame2.list get 0 end] $item]
    $w.frame.list insert 0 $item 
    $w.frame2.list delete $idx
    $w.frame3.list delete 0 end

    content_ui::map_clear $item
    content_ui::localmap_clear $item
}

frame $w.frame3 -borderwidth 10
pack $w.frame3 -side right -expand yes -fill y

scrollbar $w.frame3.scroll -command "$w.frame3.list yview"
listbox $w.frame3.list -yscroll "$w.frame3.scroll set" \
	-width 20 -height 16 -setgrid 1
pack $w.frame3.list $w.frame3.scroll -side left -fill y -expand 1

bind $w.frame3.list <ButtonRelease-1> {
    set item [selection get]
    # For some reason the search would not work... issue with unicode?
    # set idx [lsearch [$w.frame3.list get 0 end] $item]
    set idx 0
    foreach meaning [$w.frame3.list get 0 end] {
	if {[string first $item $meaning] == 0} {
    	    content_ui::localmap_set $g_last_word $g_last_idx $idx
	    break
	}
	incr idx
    }
}

###########################################################
# Framework proceudres
###########################################################

proc send_request {word outport} {
    global g_crawler

    set p_ip [ip::source]
    byRetry::init $p_ip
    byRetry::set_retry $p_ip 0
    byList::init $p_ip
    byList::set_list $p_ip [list word $word command READ meanings ""]
    byList::set_crawler $p_ip $g_crawler
    server_send $p_ip $outport
    ip::sink $p_ip
    return
}

proc send_content_save_rqst {method title data depth outport} {
    set p_ip [ip::source]
    byList::init $p_ip
    byList::set_list $p_ip [list cmd $method title $title depth $depth data $data]
    server_send $p_ip $outport
    ip::sink $p_ip
    return
}

proc send_content_read_rqst {method title depth outport} {
    set p_ip [ip::source]
    byList::init $p_ip
    byList::set_list $p_ip [list cmd $method title $title depth $depth]
    server_send $p_ip $outport
    ip::sink $p_ip
    return
}

proc reset_all {data} {
    global g_text_backup
    global w

    # Back up text data.
    set g_text_backup $data

    set wordlist [content_ui::extract_words $data]
    $w.frame0.text delete 0.0 end 
    $w.frame.list delete 0 end 
    $w.frame2.list delete 0 end 
    $w.frame3.list delete 0 end 
    $w.frame0.text insert 0.0 $data
         	
    $w.frame.list delete 0 end
    foreach word $wordlist {	
        $w.frame.list insert end $word
    }

    # Init the dictionary cache.
    content_ui::init
    return
}

proc process {inport p_ip} {
    global g_request
    global g_text_backup
    global w
    global g_chunk_list
    global g_chunk_size

    set rc ""
    if {$inport == "IN-1"} {
    	array set tmpdata [byList::get_list $p_ip]
    	set meanings $tmpdata(meanings) 
        set meanings [set_meaning $tmpdata(word) $meanings $w]
        $w.frame3.list delete 0 end
        foreach mean $meanings {
    	    $w.frame3.list insert end $mean
    	}

    } elseif {$inport == "IN-2"} {
    	array set tmpdata [byList::get_list $p_ip]
    	set cmd $tmpdata(cmd)
	if {$cmd == "Get_All_Token"} {
	    $w.frame0.text insert end $tmpdata(data)
	    $w.frame0.text insert end "\n\n"
	}

    } else {

    }
    return $rc
}

proc init {datalist} {
    global env

    Fsm::Init
    Fsm::Load_Fsm $env(COMP_HOME)/bigEnglish/content_ui/content_fsm.dat
    Fsm::Init_Fsm content_fsm
    return
}

proc shutdown {} {
}

source $env(COMP_HOME)/ip2/byList.tcl
source $env(FSM_HOME)/fsm.tcl
source $env(COMP_HOME)/bigEnglish/content_ui/content_fsm.tcl

#package require Tk
